# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pcre2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.40
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://pcre.org/"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         #"${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-wineditline"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(https://github.com/PhilipHazel/pcre2/releases/download/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2{,.sig}
        0001-RunTest-skip-a-couple-more-tests-on-Windows.patch
        0002-RunGrepTest-compare-without-paying-attention-to-line.patch
        0003-RunGrepTest-skip-test-with-absolute-Unix-path.patch
        0004-RunGrepTest-skip-script-callout-test.patch
        0005-Fix-INTERACTIVE-fd-on-Windows.patch)
sha256sums=('14e4b83c4783933dc17e964318e6324f7cae1bc75d8f3c79bc6969f00c159d68'
            'SKIP'
            '7c58ffbdf84dfc340e22c8f3e0f1a009f279843a63b64984d0bdbd948e396679'
            'e514a7df1cbc00754f0c21153406f74332787dbad12036c8d840af7fbc48f444'
            '43387a43d3919f179d3ffac6be200df66bfde5e181a79f9e48354a9d7494a895'
            'e80b0fcb8689b8444240a5d30112e9afbe1443dd541f1755904a671d4e4913a8'
            '0c38d4d88768870dab2250925cf94ec843526048fd2c0458514a640f428ff51d')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')

prepare () {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 <"${srcdir}"/0001-RunTest-skip-a-couple-more-tests-on-Windows.patch
  patch -p1 <"${srcdir}"/0002-RunGrepTest-compare-without-paying-attention-to-line.patch
  patch -p1 <"${srcdir}"/0003-RunGrepTest-skip-test-with-absolute-Unix-path.patch
  patch -p1 <"${srcdir}"/0004-RunGrepTest-skip-script-callout-test.patch
  patch -p1 <"${srcdir}"/0005-Fix-INTERACTIVE-fd-on-Windows.patch
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-jit \
    --enable-pcre2-8 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libedit

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  make check
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  # License files
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p licenses/${_realname}
  mv doc/${_realname}/COPYING licenses/${_realname}
  mv doc/${_realname}/LICENCE licenses/${_realname}
}
