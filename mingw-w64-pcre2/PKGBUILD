# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pcre2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.44
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://pcre.org/"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         #"${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-wineditline"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(https://github.com/PhilipHazel/pcre2/releases/download/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2{,.sig}
        0001-RunTest-skip-a-couple-more-tests-on-Windows.patch
        0002-RunGrepTest-compare-without-paying-attention-to-line.patch
        0003-Fix-INTERACTIVE-fd-on-Windows.patch
        0004-pcre2grep-flush-stdout-before-spawning-callouts.patch
        0005-pcre2grep-avoid-letting-Windows-handle-command-line-.patch
        0006-RunGrepTest-work-around-Unix-vs-Windows-path-issues.patch
        0007-RunGrepTest-Windows-does-not-have-a-bin-echo.patch
        0008-RunGrepTest-work-around-MSYS2-idiosyncrasy.patch)
sha256sums=('d34f02e113cf7193a1ebf2770d3ac527088d485d4e047ed10e5d217c6ef5de96'
            'SKIP'
            'bf9f757e17f72009ee04af33c4e5d153ce6f4181e3ad8381d9306019f395048d'
            'ab57fb231b459365311c77de051481bfd4b589029e5011a24a0326898a4e018d'
            '1aa92d60269a14cafb0cbb9d4767e4cc78b3655ba0da487ec306ed7a6ffb4e96'
            '10dd1d9b27446fc7ee125a92b07afd367ead3227d962c95ef40938d00df7270a'
            'e94d86c9f3b4b696aa89e4085507e748ccec722143ed3f9c6b3860abc2c5faae'
            '85aa283b61c13701713f2e56a1830d88485cda1dceeb21f3b848ff5fe1d4a68d'
            'd1680e9554bbbdb85d0719184c5ba6a6a50723ce66bd5671432166946e1ac5f4'
            'df5a2d44c999bbcadd1b13222d84e15f59c048c9eb32d305737fe50097169e54')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')

prepare () {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 <"${srcdir}"/0001-RunTest-skip-a-couple-more-tests-on-Windows.patch
  patch -p1 <"${srcdir}"/0002-RunGrepTest-compare-without-paying-attention-to-line.patch
  patch -p1 <"${srcdir}"/0003-Fix-INTERACTIVE-fd-on-Windows.patch
  patch -p1 <"${srcdir}"/0004-pcre2grep-flush-stdout-before-spawning-callouts.patch
  patch -p1 <"${srcdir}"/0005-pcre2grep-avoid-letting-Windows-handle-command-line-.patch
  patch -p1 <"${srcdir}"/0006-RunGrepTest-work-around-Unix-vs-Windows-path-issues.patch
  patch -p1 <"${srcdir}"/0007-RunGrepTest-Windows-does-not-have-a-bin-echo.patch
  patch -p1 <"${srcdir}"/0008-RunGrepTest-work-around-MSYS2-idiosyncrasy.patch
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-jit \
    --enable-pcre2-8 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libedit

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  # Work around bug in PCRE2 10.44
  test i686 != "$CARCH" ||
  for f in testdata/testoutput8-8-2 testdata/testoutput8-16-2 testdata/testoutput8-32-2
  do
    cp "../${_realname}-${pkgver}/$f" "../${_realname}-${pkgver}/$f.backup"
    perl -pi -e 's{^(Memory allocation - compiled block : )(\d+)$}{$1 . ($2 - 24)}e' "../${_realname}-${pkgver}/$f"
  done

  make check || return 1

  for f in testdata/testoutput8-8-2 testdata/testoutput8-16-2 testdata/testoutput8-32-2
  do
    test ! -f "../${_realname}-${pkgver}/$f.backup" ||
    mv "../${_realname}-${pkgver}/$f.backup" "../${_realname}-${pkgver}/$f"
  done
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  # License files
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p licenses/${_realname}
  mv doc/${_realname}/COPYING licenses/${_realname}
  mv doc/${_realname}/LICENCE licenses/${_realname}
}
